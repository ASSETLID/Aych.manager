(**************************************************************************)
(*                                                                        *)
(*    Copyright 2017 OCamlPro                                             *)
(*                                                                        *)
(*  All rights reserved. This file is distributed under the terms of the  *)
(*  GNU Lesser General Public License version 2.1, with the special       *)
(*  exception on linking described in the file LICENSE.                   *)
(*                                                                        *)
(**************************************************************************)

open OpamCudfSolverSig

let solver_backend =
  let open OpamStd.Option.Op in
  OpamStd.Config.env_string "MCCSBACKEND" >>= function
  | "glpk" -> Some `GLPK
  | "coin" -> Some `COIN_CBC
  | "coin/clp" -> Some `COIN_CLP
  | "coin/cbc" -> Some `COIN_CBC
  | "coin/symphony" -> Some `COIN_SYMPHONY
  | s when OpamStd.String.starts_with ~prefix:"lp+" s ->
    let cmd = OpamStd.String.remove_prefix ~prefix:"lp+" s in
    (match OpamSystem.resolve_command cmd with
     | None ->
       OpamConsole.warning
         "Ignored OPAMMCCSBACKEND environment variable: lp subcommand %S \
          not found"
         cmd;
       None
     | Some cmd -> Some (`LP cmd))
  | s ->
    OpamConsole.warning "Ignored unrecognised value for OPAMMCCSBACKEND: %s"
      s;
    None

let name = "builtin-"^Mccs.get_solver_id ?solver:solver_backend ()

let is_present = lazy true

let command_name = None

let default_criteria = {
  crit_default = "-removed,\
                  -count[version-lag,request],\
                  -count[version-lag,changed],\
                  -changed";
  crit_upgrade = "-removed,\
                  -count[version-lag,solution],\
                  -new";
  crit_fixup = "-changed,-count[version-lag:,false]";
  crit_best_effort_prefix = Some "+count[opam-query:,false],";
}

let call ~criteria ?timeout cudf =
  match
    Mccs.resolve_cudf
      ?solver:solver_backend
      ~verbose:OpamCoreConfig.(!r.debug_level >= 2)
      ?timeout criteria cudf
  with
  | None -> raise Common.CudfSolver.Unsat
  | Some (preamble, univ) -> Some preamble, univ
  | exception Mccs.Timeout -> raise Timeout
